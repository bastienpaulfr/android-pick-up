plugins {
    id "fr.coppernic.versioning" version "3.1.2"
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

apply from: rootProject.file('gradle/ktlint.gradle')
apply from: rootProject.file('gradle/jacoco.gradle')

repositories {
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

android {
    compileSdkVersion 31

    namespace 'fr.bipi.lib.pickup'

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 31
        versionCode versioning.info.versionCode
        versionName versioning.info.display

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Sometimes we are including libs that contains packages that are not in Android framework
    lintOptions {
        warning 'InvalidPackage'
    }

    // Needs this for Robolectric 4+
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    // Used for including libs compiled with kotlin
    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
        exclude 'META-INF/lib_release.kotlin_module'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Logging
    implementation "org.slf4j:slf4j-api:1.7.32"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    //noinspection GradleDependency 4.0.x is conflicting with hamcrest and needs java 8
    testImplementation "org.awaitility:awaitility:3.0.0"
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'org.robolectric:robolectric:4.6.1'
    testImplementation 'org.amshove.kluent:kluent:1.65'
    testImplementation 'androidx.test:core:1.4.0'

    // Timber
    testImplementation('com.jakewharton.timber:timber:5.0.1') {
        exclude group: 'org.jetbrains'
    }
    testImplementation('com.github.bastienpaulfr:Treessence:1.0.5') {
        exclude group: 'org.jetbrains'
    }
    testImplementation('com.arcao:slf4j-timber:3.1') {
        exclude group: 'org.jetbrains'
    }

    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    //noinspection GradleDependency 4.0.x is conflicting with hamcrest and needs java 8
    androidTestImplementation "org.awaitility:awaitility:3.0.0"

    // Timber
    androidTestImplementation('com.jakewharton.timber:timber:5.0.1') {
        exclude group: 'org.jetbrains'
    }
    androidTestImplementation('com.github.bastienpaulfr:Treessence:1.0.5') {
        exclude group: 'org.jetbrains'
    }
    androidTestImplementation('com.arcao:slf4j-timber:3.1') {
        exclude group: 'org.jetbrains'
    }
}

apply from: rootProject.file('gradle/publish-lib.gradle')
